// SPDX-License-Identifier: GPL-3.0-or-later

plugins {
    id('com.android.library')
}

android {
    namespace 'io.github.muntashirakon.AppManager.docs'
    compileSdk compile_sdk

    defaultConfig {
        minSdk min_sdk
        targetSdk target_sdk
    }

    compileOptions {
        encoding "UTF-8"
    }
}

tasks.register('buildDocs') {
    doLast {
        println("=== docs: start ===")
        String buildExec = "${rootProject.projectDir.absolutePath}/scripts/make_docs.sh"
        exec {
            workingDir = project.rootDir
            executable = buildExec
        }
        println("=== docs: finish ===")
    }
}

tasks.register('cleanupDocs') {
    doLast {
        File file = new File("${rootProject.projectDir.absolutePath}/docs/src/main/res")
        if (file.exists()) {
            deleteDir(file)
        }
    }
}

def deleteDir(File dir) {
    if (dir != null && dir.isDirectory()) {
        String[] children = dir.list()
        if (children == null) return false
        for (String child : children) {
            boolean success = deleteDir(new File(dir, child))
            if (!success) return false
        }
        return dir.delete()
    } else if (dir != null && dir.isFile()) {
        return dir.delete()
    } else return false
}

preBuild.dependsOn buildDocs
clean.dependsOn cleanupDocs