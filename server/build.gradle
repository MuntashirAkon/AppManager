// SPDX-License-Identifier: GPL-3.0-or-later

apply from: "$gradle.ext.appManagerRoot/common.gradle"

dependencies {
    compileOnly project(path: ':hiddenapi')
    api project(path: ':libserver')
}

static def toPath(String s) {
    if (isWin()) {
        return s.replace('/', '\\')
    } else {
        return s
    }
}

static def isWin() {
    return org.gradle.internal.os.OperatingSystem.current().isWindows()
}

String.metaClass.toPath = { -> toPath(delegate) }

ext {
    debugClassDir = project.rootDir.absolutePath + '/server/build/intermediates/javac/debug/classes'.toPath()
    libserverClassDir = project.rootDir.absolutePath + '/libserver/build/intermediates/javac/debug/classes'.toPath()
    hiddenApiClassDir = project.rootDir.absolutePath + '/hiddenapi/build/classes/java/main'.toPath()
    amJar = project.rootDir.absolutePath + '/app/src/main/assets/am.jar'.toPath()
    mainJar = project.rootDir.absolutePath + '/app/src/main/assets/main.jar'.toPath()
}

build.doLast {
    if (!isModified()) {
        println("Files aren't modified, skip creating jar files.")
        return
    }
    println("Build success, creating am.jar...")
    def buildArgs = ['--release', '--output', amJar]
    buildArgs.addAll(get_args(libserverClassDir, ["io/github/muntashirakon/AppManager/server/common/"]))
    buildArgs.addAll(get_args(debugClassDir,  ["io/github/muntashirakon/AppManager/server/"]))
    buildArgs.addAll(hiddenApiClassDir +  "/android/app/ActivityThread.class")
    buildArgs.addAll(['--lib', "${android.sdkDirectory.path}/platforms/android-${rootProject.ext.targetSdk}/android.jar"])
    exec {
        workingDir = project.rootDir
        executable = file("${android.sdkDirectory.path}/build-tools/${android.buildToolsVersion}/d8${isWin() ? '.bat' : ''}".toPath())
        args = buildArgs
    }
    println("Created ${amJar}")
    println("Creating main.jar...")
    buildArgs = ['--release', '--output', mainJar]
    buildArgs.add("${debugClassDir}/io/github/muntashirakon/AppManager/server/IPCMain.class")
    buildArgs.add("${libserverClassDir}/io/github/muntashirakon/AppManager/server/common/ServerUtils.class")
    buildArgs.addAll(['--lib', "${android.sdkDirectory.path}/platforms/android-${rootProject.ext.targetSdk}/android.jar"])
    exec {
        workingDir = project.rootDir
        executable = file("${android.sdkDirectory.path}/build-tools/${android.buildToolsVersion}/d8${isWin() ? '.bat' : ''}".toPath())
        args = buildArgs
    }
    println("Created ${mainJar}")
}

def isModified() {
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir = project.rootDir
        executable = "${project.rootDir}/scripts/check_am_server.sh"
        standardOutput = stdout
    }
    return stdout.toString().trim() == "M"
}

def get_args(String path, List pkgPath) {
    def ret = []
    pkgPath.forEach {
        it ->
            def file = new File(path, it.toPath())
            loadClsFiles(file, ret)
    }
    return ret
}

def loadClsFiles(File f, List paths) {
    File[] files = f.listFiles()
    if (files != null) {
        files.each {
            file ->
                if (file.file) {
                    if (file.name.endsWith(".class") && !file.name.startsWith("IPCMain")) {
                        paths.add(file.path)
                    }
                } else {
                    loadClsFiles(file, paths)
                }
        }
    }
}

task cleanupJar() {
    doLast {
        file("${rootProject.projectDir.absolutePath}/app/src/main/assets/am.jar").delete()
        file("${rootProject.projectDir.absolutePath}/app/src/main/assets/main.jar").delete()
    }
}

clean.dependsOn cleanupJar
