/*
 * Copyright (C) 2020 Muntashir Al-Islam
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools
    ndkVersion rootProject.ext.ndk

    defaultConfig {
        applicationId 'io.github.muntashirakon.AppManager'
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 374
        versionName "2.5.20"
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            storePassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyPassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyAlias 'key0'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "App Manager"
        }
        preRelease {
            minifyEnabled false
            versionNameSuffix '-PRE'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "AM Pre-release"
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "AM Debug"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.contains("Debug") || it.contains("PreRelease") }
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }
    }
    aaptOptions {
        noCompress 'jar', 'sh'
    }
}

dependencies {
    compileOnly project(path: ':HiddenApi')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':libApkParser')
    implementation project(path: ':libOpenPGP')
    implementation project(path: ':sunJava')
    implementation project(path: ':toybox')

    implementation 'com.github.MuntashirAkon:apksig-android:178a05e4c9'
    implementation 'com.github.MuntashirAkon:lazysodium-android:4.3.0@aar'

    implementation 'com.google.android.material:material:1.3.0-alpha04'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.preference:preference:1.1.1'

    implementation "com.github.YarikSOffice:lingver:1.3.0"

    implementation 'net.java.dev.jna:jna:5.6.0@aar'

    implementation "com.github.topjohnwu.libsu:core:${libsu}"
    implementation 'com.tananaev:adblib:1.2'

    implementation 'me.weishu:free_reflection:3.0.1'

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
}

preBuild.dependsOn ":server:build"
